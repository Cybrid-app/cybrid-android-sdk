plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
    id 'signing'
    id 'org.jetbrains.kotlinx.kover'
}

def filesExcluded = [
        'app.cybrid.sdkandroid.components.*',
        'app.cybrid.sdkandroid.BuildConfig',
        'app.cybrid.sdkandroid.core.Constants',
]

apply from: '../version.gradle'

group 'app.cybrid'
version sdk_version

android {
    compileSdk 33

    defaultConfig {
        minSdk 26
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }
    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    publishing {
        singleVariant('release') {}
        singleVariant('debug') {
            withSourcesJar()
            withJavadocJar()
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose = true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = '1.4.3'
    }
    testOptions {
        unitTests.returnDefaultValues = true
        unitTests {
            includeAndroidResources = true
        }
        unitTests.all {
            if (name == "testDebugUnitTest") {
                kover {}
            }
        }
    }
    namespace 'app.cybrid.sdkandroid'
    buildToolsVersion '31.0.0'
}

tasks.withType(Test) {
    jvmArgs = ["--add-opens", "java.base/java.lang.invoke=ALL-UNNAMED"]
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {

    kotlinOptions.jvmTarget = "1.8"
    kotlinOptions.freeCompilerArgs = ["-Xjvm-default=all-compatibility"]
}

kover {

    engine = kotlinx.kover.api.DefaultIntellijEngine.INSTANCE
    instrumentation {
        excludeTasks.add "testReleaseUnitTest"
    }
    htmlReport {
        onCheck.set(false)
    }
    xmlReport {
        onCheck.set(false)
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId 'app.cybrid'
                artifactId = 'cybrid-android-sdk'
                version version
                pom {
                    name = 'Cybrid Android SDK'
                    description = 'Android SDK for UI Components'
                    url = 'https://cybrid.xyz'
                    licenses {
                        license {
                            name = 'Apache-2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'erick-cybrid'
                            name = 'Erick Sanchez'
                            email = 'erick.sanchez@cybrid.app'
                        }
                    }
                    scm {
                        url = 'https://github.com/Cybrid-app/cybrid-sdk-android'
                    }
                }
            }
        }
    }
}

signing {

    System.out.println("Publish version ${version}")
    def signingPassword = System.getenv('gradle_sonatype_PASSPHRASE')
    def signingKey = null
    if(System.getenv('gradle_sonatype_SECRET')!= null) {
        signingKey = new String(System.getenv('gradle_sonatype_SECRET').decodeBase64())
    }
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications
}

dependencies {

    // -- Vars
    ext.retrofitVersion = '2.9.0'

    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    // -- Cybrid API Bank
    implementation('app.cybrid:cybrid-api-bank-kotlin:0.71.68') {
        exclude group:'org.apache.oltu.oauth2', module: 'org.apache.oltu.oauth2.common'
    }

    // -- Network
    implementation 'com.google.code.gson:gson:2.9.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.9'
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-scalars:$retrofitVersion"

    // -- Compose
    implementation 'androidx.compose.ui:ui:1.4.3'
    implementation 'androidx.compose.ui:ui-util:1.4.3'
    implementation 'androidx.compose.ui:ui-tooling:1.4.3'
    implementation 'androidx.compose.foundation:foundation:1.4.3'
    implementation 'androidx.compose.material:material:1.4.3'
    implementation 'androidx.compose.runtime:runtime-livedata:1.4.3'
    implementation 'androidx.compose.ui:ui-test-junit4:1.3.3'
    implementation 'androidx.activity:activity-compose:1.7.2'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.6.1'
    implementation 'androidx.constraintlayout:constraintlayout-compose:1.0.1'
    implementation 'androidx.compose.material:material-icons-extended:1.4.3'
    implementation 'io.coil-kt:coil-compose:2.2.2'

    // -- Compose Testing
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4:1.3.3'
    testImplementation 'androidx.compose.ui:ui-test-junit4:1.3.3'
    debugImplementation 'androidx.compose.ui:ui-test-manifest:1.3.3'
    debugImplementation "androidx.customview:customview-poolingcontainer:1.0.0"

    // -- Lifecycle
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.5.1"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.5.1"
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.activity:activity-ktx:1.6.1'
    implementation 'androidx.fragment:fragment-ktx:1.5.5'

    // -- Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'

    // -- Persona
    implementation 'com.withpersona.sdk2:inquiry:2.2.37'

    // -- Plaid
    implementation 'com.plaid.link:sdk-core:3.10.1'

    // -- Project Testing
    testImplementation 'com.android.support.test:runner:1.0.2'
    testImplementation "io.mockk:mockk:1.13.3"
    testImplementation 'org.robolectric:robolectric:4.9'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4'
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}